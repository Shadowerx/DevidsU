[ExtensionOf(tableStr(SalesQuotationLine))]
final public class SalesQuotationLine_Extension
{
    /// <summary>
    /// Initializes SalesQuotationLine, NetPrice field from PriceDiscTable if checkbox is checked then discount is not calculated
    /// </summary>
    /// <param name="_pricedisc">
    /// priceDisc class
    /// </param>
    /// <param name="_checkPolicy">
    /// priceDiscPolicy
    /// </param>
    /// <param name="_retailcalculator>
    /// RetailSalesQuiotationCalc
    /// </param>
    /// <returns>
    /// </returns>
    /// <remarks>
    /// C - By Dēvids Ulanovs, 03-19-2019, MOD101_NetSalesPrice
    /// </remarks>
    void initFromPriceDisc(PriceDisc_Price _priceDisc, PriceDiscPolicyCheckPolicy _checkPolicy, boolean _initDateFields, RetailSalesQuotationCalculator _retailCalculator)
    {
            
        InventDim           inventDim;
        InventDimParm       inventDimParm;
        InventDimParm       inventDimParmAllowBlank;

        next initFromPriceDisc(_priceDisc, _checkPolicy, _initDateFields,_retailCalculator);

        PriceDiscTable  priceDiscTable;
        inventDimParm.initFromInventDim(this.inventDim());
        inventDimParmAllowBlank.toggleAllInventDim();

        select firstonly priceDiscTable
            index hint PriceDiscIdx
            where priceDiscTable.Relation           == PriceType::PriceSales
            && ( (priceDiscTable.ItemCode == PriceDiscProductCodeType::Table && priceDiscTable.ItemRelation == this.ItemId)
                    || ((priceDiscTable.ItemCode == PriceDiscProductCodeType::GroupId && priceDiscTable.ItemRelation == InventTable::find(this.ItemId).itemGroupId())
                    || priceDiscTable.ItemCode == PriceDiscProductCodeType::All))
            && ( (priceDiscTable.AccountCode == PriceDiscPartyCodeType::Table && priceDiscTable.AccountRelation == this.CustAccount)
            || (priceDiscTable.AccountCode == PriceDiscPartyCodeType::GroupId && priceDiscTable.AccountRelation == this.SalesGroup)
            || (priceDiscTable.AccountCode == PriceDiscPartyCodeType::All))
            && priceDiscTable.Currency           == this.CurrencyCode
            && priceDiscTable.UnitId             == this.SalesUnit
            && priceDiscTable.QuantityAmountFrom   <= this.SalesQty
            && (priceDiscTable.QuantityAmountTo > this.SalesQty || !this.SalesQty)
            #InventDimExistsJoinAllowBlank(priceDiscTable.inventDimId, inventDim, this.inventDim(), inventDimParm, inventDimParmAllowBlank);

        this.NetPrice = priceDiscTable.NetPrice;
        
        if(priceDiscTable.NetPrice ==  NoYes::Yes)
        {
            this.lineDisc = 0;
            this.LinePercent = 0;

        }
    }

    /// <summary>
    /// If NetPrice is checked then discount is 0
    /// </summary>
    /// <param name="_fieldId">
    /// fieldId
    /// </param>
    /// <returns>
    /// </returns>
    /// <remarks>
    /// C - By Dēvids Ulanovs, 03-19-2019, MOD101_NetSalesPrice
    /// </remarks>
    public void modifiedField(FieldId _fieldId, boolean _userInput)
    {
        next modifiedField(_fieldId, _userInput);
        switch (_fieldId)
        {
            case fieldNum(SalesquotationLine, NetPrice):
                if (this.NetPrice == NoYes::Yes)
                {
                    this.LineDisc = 0;
                    this.LinePercent = 0;
                    this.calcLineAmount();
                }
                break;
        }
    }

}
